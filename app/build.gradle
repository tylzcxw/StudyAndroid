apply plugin: 'com.android.application'
/**采用解析Properties文件获取签名信息的方式，*/
Properties props = new Properties();
props.load(new FileInputStream(file("signing.properties")))

def releaseTime() {
    return new Date().format('yyyyMMddHHmm')
}
android {
    compileSdkVersion 26
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "tylz.study"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        multiDexEnabled true
    }
    signingConfigs { //签名文件配置
        debug {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
        }
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
        }
    }
    buildTypes {
        release {
            minifyEnabled false//是否开启混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "${defaultConfig.applicationId}_" + buildType.name + "_v${defaultConfig.versionName}_${releaseTime()}.apk";
            }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile project(':framework')
    compile project(':protocol')
    compile project(':common')
}
